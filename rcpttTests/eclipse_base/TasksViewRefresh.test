--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _NXS_4F0MEeGDGrudSYLu8w,_okXLoFqJEeSjvvK1muLiaA
Element-Name: TasksViewRefresh
Element-Type: testcase
Element-Version: 3.0
External-Reference: https://github.com/eclipse-rcptt/org.eclipse.rcptt/issues/195
Id: _PmlX0FMiEfCmMtW6w_hYvg
Runtime-Version: 2.6.0.202506262126
Save-Time: 6/28/25, 12:28â€¯AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Copyright (c) 2025 Xored Software Inc and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v2.0
which accompanies this distribution, and is available at
https://www.eclipse.org/legal/epl-v20.html

Contributors:
    Xored Software Inc - initial creation and/or initial documentation
--------------------------------------------------------------------------------

Test steps:
Add tasks alternating between via view context menu and Java editor
Ensure the Tasks view shows them properly without a need to explicitly wait for background activity
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-view "Test Explorer" | get-tree | select "project/src/Main.java" | double-click


proc -name clear  -body {
	try -times 10 -delay 0 -command {
	    verify-error {
	        get-view Tasks | get-tree | select "test task" -column Description | get-menu -path Delete | click
	        get-window "Delete Selected Entries" | get-button Delete | click
	    }
	}
}
clear
repeat -times 10 -command {
    with [get-editor "Main.java" | get-text-viewer] {
	    set-text ""
	    key-type "M1+s"
    }
    get-view Tasks | get-tree | get-property itemCount | equals 0 | verify-true
	get-view Tasks | get-tree | get-menu -path "Add Task..." | click
	with [get-window "Add Task"] {
	    get-editbox -after [get-label "Description:"] | set-text "test task"
	    get-button Add | click
	}
    with [get-editor "Main.java" | get-text-viewer] {
        set-text "// TODO Show me in Tasks view"
        key-type "M1+s"
     }
	get-view Tasks | get-tree | get-property itemCount | equals 2 | verify-true
	get-view Tasks | get-tree | select "test task" -column Description | get-menu -path Delete | click
    get-window "Delete Selected Entries" | get-button Delete | click
    get-view Tasks | get-tree | get-property itemCount | equals 1 | verify-true
}

clear
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
